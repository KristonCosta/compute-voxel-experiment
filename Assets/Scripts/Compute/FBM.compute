// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel Basic
#include "Include/Noise2D.compute"
#include "Include/BlockTypes.compute"
#ifdef GL_ES
precision highp float;
#endif


RWStructuredBuffer<uint> points;
uint size_of_chunk;
uint height_of_chunk;
float3 offset;

float inverse_lerp(float start, float end, float value)
{
    if (start == end) return 1.0;
    return clamp((value - start) / (end - start), 0.0, 1.0); 
}

float map_value(float newMin, float newMax, float originalMin, float originalMax, float value)
{
    return lerp(newMin, newMax, inverse_lerp(originalMin, originalMax, value));
}

float fractal_brownian_motion(float2 pos, int octave, float persistence)
{
    float total = 0;
    float frequency = 1.0;
    float amplitude = 1.0;
    float max_value = 0;
    float neg_offset = 32000;

    for (int i = 0; i < octave; i++)
    { 
        total += map_value(0, 1, -0.707, 0.707, snoise((pos + neg_offset) * frequency)) * amplitude;
        max_value += amplitude;
        amplitude *= persistence;
        frequency *= 2;
    }

    return total / max_value;
}

float fractal_brownian_motion_3d(float3 pos, float smoothing, int octave)
{
    float xy = fractal_brownian_motion(pos.xy * smoothing, octave, 0.5);
    float yz = fractal_brownian_motion(pos.yz * smoothing, octave, 0.5);
    float xz = fractal_brownian_motion(pos.xz * smoothing, octave, 0.5);

    float yx = fractal_brownian_motion(pos.yx * smoothing, octave, 0.5);
    float zy = fractal_brownian_motion(pos.zy * smoothing, octave, 0.5);
    float zx = fractal_brownian_motion(pos.zx * smoothing, octave, 0.5);

    return (xy + yz + xz + yx + zy + zx) / 6.0f;
}

int generate_height(float2 pos)
{
    uint halfway = height_of_chunk / 2;
    uint jitter = height_of_chunk / 20;
    return lerp(halfway - jitter, halfway + jitter, fractal_brownian_motion(pos * 0.005f, 4, 0.5 ));
}

int generate_stone_height(float2 pos) 
{
    uint halfway = height_of_chunk / 2;
    uint jitter = height_of_chunk / 20 - 1;
    return lerp(halfway - jitter, halfway + jitter, fractal_brownian_motion(pos * 0.005f, 5, 0.5 ));
}

uint index_from_coord(uint3 coord) {
    return coord.z * height_of_chunk * size_of_chunk + coord.y * size_of_chunk + coord.x;
}

[numthreads(8, 8, 8)]
void Basic(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= size_of_chunk || id.z >= size_of_chunk || id.y >= height_of_chunk) {
        return;
    }
    float3 translated = id + offset;
    uint result = UNDEFINED;
    result = fractal_brownian_motion_3d(translated, 0.005, 3) < 0.46f ? AIR : result;
    if (result == UNDEFINED)
    {
        uint height = generate_stone_height(translated.xz);
        if (translated.y < height)
        {
            result = STONE;
        }
    }
    
    if (result == UNDEFINED)
    {
        uint height = generate_height(translated.xz);
        if (translated.y == height)
        {
            result = GRASS;
        }

        if (translated.y < height)
        {
            result = DIRT;
        }
    }

    if (result == UNDEFINED)
    {
        result = AIR;
    }
    
    
    // result = result == UNDEFINED && translated.y == height ? GRASS : GRASS;
    // result = result == UNDEFINED && translated.y < height ? DIRT : AIR;
    points[index_from_coord(id)] = result;
}
